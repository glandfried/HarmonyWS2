## This file is Appendix A of the article "Bayesian estimation of herd-level prevalence and risk factors associated with BoHV-1 infection in cattle herds in the State of Para√≠ba, Brazil"

## This JAGS model code is intended to be run from R using the runjags package - see:
### Denwood (2016). runjags: An R Package Providing Interface Utilities, Model Templates, Parallel Computing Methods and Additional Distributions for MCMC Models in JAGS. 
### Journal of Statistical Software, 71(9), 1-25. doi:10.18637/jss.v071.i09

## Parts of the code relevant to the stochastic variable selection are specifically indicated
## Feel free to contact the authors of the paper (and/or maintainer of the runjags package) for further information or possible collaboration on similar analyses

model{
	
	for(i in 1:NFarms){
		NumberPositive[i] ~ dbinom(probability[i], TotalAnimals[i])

		# infectedprev is common across all infected farms for identifiability reasons:
		farmprev[i] <- infectedprev * farminfected[i]
		probability[i] <- (farmprev[i] * sensitivity) + ((1-farmprev[i]) * (1-specificity))
		
		# binary infection status of the farm:
		farminfected[i] ~ dbern(infectionrisk[i])
		
		logit(infectionrisk[i]) <- intercept + sum(allriskeffects[i,])
		
		# Loop over the relevant risk factors, using an indicator variable (RiskIndex) to index the relevant risk factor level for each farm and risk factor:
		for(j in 1:NRisks){
			allriskeffects[i,j] <- riskeffect[RiskIndex[i,j]] * riskonoff[j]
			# this line implements the stochastic variable selection - if the risk factor is set to off then it has no effect here
		}
		
	}

	meaninfectionprob <- mean(farminfected)

	# Priors:
	intercept ~ dnorm(0, 10^-6)
	for(l in 1:NRiskLevels){
		riskeffect[l] ~ dnorm(0, 1/4)
		# Note: risk effects corresponding to reference categories are set as 0 in data and are therefore not estimated
	}
	for(r in 1:NRisks){
		# this line implements the stochastic variable selection - each risk factor is turned on/off for all farms at once:
		riskonoff[r] ~ dbern(0.25)
	}
	
	# Priors for test characteristics:
	specificity ~ dbeta(78, 4)
	sensitivity ~ dbeta(33.5, 2.7)

	infectedprev ~ dbeta(1,1)
	
	#data# TotalAnimals, NumberPositive, NFarms, RiskIndex, NRisks, NRiskLevels, riskeffect
	#monitor# specificity, sensitivity, riskeffect, riskonoff, infectedprev, meaninfectionprob
	
	#inits# .RNG.name, .RNG.seed
	#modules# glm
}
